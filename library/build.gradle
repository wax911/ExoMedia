apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

afterEvaluate {
    apply from: 'gradle/publish.gradle'
}

/**
 * Holds the information associated with the library needed for the
 * bintray plugin to publish the artifact
 */
class LibraryInfo {
    static Integer versionMajor = 5
    static Integer versionMinor = 0
    static Integer versionPatch = 1

    static String artifactId = 'exomedia'
    static String groupId = 'com.devbrackets.android'
    static String versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
    static Integer versionCode = versionMajor * 10_000 + versionMinor * 1_000 + versionPatch * 100
}

static def getLibraryInfo() {
    return new LibraryInfo()
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    implementation 'androidx.media:media:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'

    // ExoPlayer
    api "com.google.android.exoplayer:exoplayer-core:$rootProject.ext.exoPlayerVersion"
    api "com.google.android.exoplayer:exoplayer-dash:$rootProject.ext.exoPlayerVersion"
    api "com.google.android.exoplayer:exoplayer-hls:$rootProject.ext.exoPlayerVersion"
    api "com.google.android.exoplayer:exoplayer-smoothstreaming:$rootProject.ext.exoPlayerVersion"

    testImplementation 'junit:junit:4.13'
    testImplementation "com.google.truth:truth:1.0"
    testImplementation "org.robolectric:robolectric:4.3.1"
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29

        versionCode getLibraryInfo().versionCode
        versionName getLibraryInfo().versionName

        vectorDrawables.useSupportLibrary = true
    }


    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}